REQUIRE "view3d.a4c";

MODEL cavity1 REFINES view3d;
	h_window, h_sides, h_top IS_A heat_transfer_coefficient;
	T_amb IS_A temperature;
	T_tubes IS_A temperature;
	s_tubes IS_A set OF symbol_constant;

	s_top, s_left, s_right, s_window IS_A set OF symbol_constant;
	s_top :== ['AB0','AB1','AB2','AB3','AB4','AB5','AB6','AB7','AB8','AB9','AB10','AB11'];
	s_left :== ['BC0','BC1','BC2','BC3'];
	s_window :== ['CD0','CD1','CD2','CD3','CD4','CD5','CD6','CD7','CD8','CD9','CD10','CD11'];
	s_right :== ['DA0','DA1','DA2','DA3'];

	n_top, n_sides, n_window IS_A integer_constant;
	n_top :== CARD[s_top];
	n_sides :== CARD[s_left] + CARD[s_right];
	n_window :== CARD[s_window];

	eps_window ALIASES eps['CD0'];
	eps_tubes ALIASES eps['p0s0'];

	s_tubes :== n - s_top - s_left - s_window - s_right;
	T[s_tubes], T_tubes ARE_THE_SAME;

	q_window_rad[s_window] IS_A energy_rate_per_length;
	q_window_conv[s_window] IS_A energy_rate_per_length;

	FOR i IN s_window CREATE
		q_window_conv[i] = h_window * A[i] * (T[i] - T_amb);
		q_window_rad[i] = A[i] * eps[i] * 1{SIGMA_C} * (T[i]^4 - T_amb^4);
		-q[i] = q_window_rad[i] + q_window_conv[i];
	END FOR;

	FOR i IN s_left + s_right CREATE
		-q[i] = h_sides * A[i] * (T[i] - T_amb);
	END FOR;

	FOR i IN s_top CREATE
		-q[i] = h_top * A[i] * (T[i] - T_amb);
	END FOR;

	T_window_avg IS_A temperature;
	T_window_avg = SUM[T[i] | i IN s_window] / n_window	;

	T_top_avg IS_A temperature;
	T_top_avg = SUM[T[i] | i IN s_top] / n_top;

	T_side_avg IS_A temperature;
	T_side_avg = SUM[T[i] | i IN s_left + s_right] / n_sides;

	F_equiv IS_A factor;
	q_equiv_int, q_equiv_ext_c, q_equiv_ext_r IS_A energy_rate_per_length;
	q_equiv_int = F_equiv / (1./eps_window + 1./eps_tubes - 1) * 1{SIGMA_C} * (T_tubes^4 - T_window_avg^4);

	A_window IS_A distance;
	A_window = SUM[A[i] | i IN s_window];
	
	q_equiv_ext_c = h_window * A_window * ( T_window_avg - T_amb);
	q_equiv_ext_r = A_window * eps_window * 1{SIGMA_C} * (T_window_avg^4 - T_amb^4);

	q_equiv_int = q_equiv_ext_c + q_equiv_ext_r;

METHODS
METHOD on_load;
	RUN reset;
	RUN values;
END on_load;
METHOD specify;
	RUN view3d::specify;
	FIX T_tubes;
	FREE q[n];
	FREE T['1','2','3','4'];
	FIX T_amb;
	FIX h_sides, h_window, h_top;
END specify;
METHOD values;
	RUN view3d::values;
	T_tubes := 250 {K} + 273.15 {K};

	T_amb := 290 {K};
	h_sides := 0.5 {W/m^2/K};
	h_top := 0.5 {W/m^2/K};
	h_window := 10 {W/m^2/K};
END values;

END cavity1;

