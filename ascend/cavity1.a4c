REQUIRE "view3d.a4c";

MODEL cavity1 REFINES view3d;
	h_window, h_sides, h_top IS_A heat_transfer_coefficient;
	T_amb IS_A temperature;
	T_tubes IS_A temperature;
	n_tubes IS_A set OF symbol_constant;

	(* this is insanity; unfortunately the cavity model is set up to take
	a set of symbol_constant instead of numbers *)
	n_top, n_left, n_right, n_window IS_A set OF symbol_constant;
	n_top :==['1','2','3','4','5','6','7','8','9','10','11','12'];
	n_left :== ['13','14','15','16'];
	n_window :== ['17','18','19','20','21','22','23','24','25','26','27','28'];
	n_right :== ['29','30','31','32'];

	eps_window ALIASES eps['17'];
	eps_tubes ALIASES eps['128'];

	n_tubes :== n - n_top - n_left - n_window - n_right;
	T[n_tubes], T_tubes ARE_THE_SAME;

	q_window_rad[n_window] IS_A energy_rate_per_length;
	q_window_conv[n_window] IS_A energy_rate_per_length;

	FOR i IN n_window CREATE
		q_window_conv[i] = h_window * A[i] * (T[i] - T_amb);
		q_window_rad[i] = A[i] * eps[i] * 1{SIGMA_C} * (T[i]^4 - T_amb^4);
		-q[i] = q_window_rad[i] + q_window_conv[i];
	END FOR;

	FOR i IN n_left + n_right CREATE
		-q[i] = h_sides * A[i] * (T[i] - T_amb);
	END FOR;

	FOR i IN n_top CREATE
		-q[i] = h_top * A[i] * (T[i] - T_amb);
	END FOR;

	T_window_avg IS_A temperature;
	T_window_avg = SUM[T[i] | i IN n_window] / 12.	;

	T_top_avg IS_A temperature;
	T_top_avg = SUM[T[i] | i IN n_top] / 12.;

	T_side_avg IS_A temperature;
	T_side_avg = SUM[T[i] | i IN n_left + n_right] / 8.;

	F_equiv IS_A factor;
	q_equiv_int, q_equiv_ext_c, q_equiv_ext_r IS_A energy_rate_per_length;
	q_equiv_int = F_equiv / (1./eps_window + 1./eps_tubes - 1) * 1{SIGMA_C} * (T_tubes^4 - T_window_avg^4);

	A_window IS_A distance;
	A_window = SUM[A[i] | i IN n_window];
	
	q_equiv_ext_c = h_window * A_window * ( T_window_avg - T_amb);
	q_equiv_ext_r = A_window * eps_window * 1{SIGMA_C} * (T_window_avg^4 - T_amb^4);

	q_equiv_int = q_equiv_ext_c + q_equiv_ext_r;

METHODS
METHOD on_load;
	RUN reset;
	RUN values;
END on_load;
METHOD specify;
	RUN view3d::specify;
	FIX T_tubes;
	FREE q[n];
	FREE T['1','2','3','4'];
	FIX T_amb;
	FIX h_sides, h_window, h_top;
END specify;
METHOD values;
	RUN view3d::values;
	T_tubes := 250 {K} + 273.15 {K};

	T_amb := 290 {K};
	h_sides := 0.5 {W/m^2/K};
	h_top := 0.5 {W/m^2/K};
	h_window := 10 {W/m^2/K};
END values;

END cavity1;

